import static com.github.amkay.gradle.gitflow.version.VersionType.*

def readmePath     = "${project.projectDir}/README.asciidoc"
def githubPagesUrl = "https://${githubUser}.github.io/${project.name}"
def releaseDocPath = 'release/{version}/doc'
def developDocPath = 'develop/doc'
def docPath


if (version.type == RELEASE) {
    docPath = releaseDocPath
} else {
    docPath = developDocPath
}


task bumpVersionInReadme {
    description 'Bumps the version in the README.asciidoc file and sets the path to the documentation.'

    doLast {
        def newReadme = new File("${readmePath}.new")

        newReadme.withWriter('utf-8') { writer ->
            new File(readmePath).eachLine { line ->
                if (line.startsWith(':version:')) {
                    line = ":version: ${version}"
                } else if (line.startsWith(':doc-path:')) {
                    line = ":doc-path: $githubPagesUrl/$docPath"
                }

                writer.writeLine line
            }
        }

        newReadme.renameTo readmePath
    }
}
