import static com.github.amkay.gradle.gitflow.version.VersionType.*

def releaseDestination = "release/$version"
def developDestination = 'develop'
def destination


if (version.type == RELEASE) {
    destination = releaseDestination
} else {
    destination = developDestination
}


githubPages {
    commitMessage "Publish of Github pages from Gradle for ${version}."
    deleteExistingFiles false

    pages {
        into destination

        with copySpec {
            into 'doc'

            with copySpec {
                from javadoc
                into 'javadoc'
            }

            if (project.hasProperty('groovydoc')) {
                with copySpec {
                    from groovydoc
                    into 'groovydoc'
                }
            }
        }

        if (version.type != RELEASE) {
            with copySpec {
                from jar, javadocJar, sourcesJar
            }

            if (project.hasProperty('groovydocJar')) {
                with copySpec {
                    from groovydocJar
                }
            }
        }
    }

    repoUri "https://github.com/${rootProject.githubUser}/${rootProject.name}.git"

    credentials {
        username = githubApiKey
        password = ''
    }
}

